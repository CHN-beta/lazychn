# Copyright 1999-2020 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=7

inherit desktop multilib xdg

DESCRIPTION="Wolfram Mathematica"
SRC_URI="Mathematica_${PV}_LINUX.sh"
HOMEPAGE="https://www.wolfram.com/mathematica/"

LICENSE="all-rights-reserved"
KEYWORDS="-* ~amd64"
SLOT="0"
IUSE="+doc"

RESTRICT="strip mirror bindist fetch binchecks"

DEPEND="sys-libs/libomp
	media-libs/alsa-lib
	dev-libs/atk
	x11-libs/cairo
	media-video/ffmpeg
	media-libs/fontconfig
	x11-libs/gdk-pixbuf
	dev-libs/glib
	media-libs/glu
	dev-libs/gmime
	dev-libs/gmp
	x11-libs/gtk+:2
	media-libs/harfbuzz
	dev-cpp/tbb
	virtual/jre
	virtual/jdk
	media-libs/leptonica
	dev-libs/libbson
	dev-libs/libffi
	dev-libs/mongo-c-driver
	media-libs/libogg
	media-libs/libpng-compat:1.2
	sys-libs/libselinux
	x11-libs/libSM
	net-libs/libssh2
	sys-apps/util-linux
	x11-libs/libX11
	x11-libs/libXcomposite
	dev-libs/libxml2
	x11-libs/libXrandr
	dev-libs/libxslt
	x11-libs/libXScrnSaver
	x11-libs/libXtst
	x11-libs/libXxf86vm
	x11-apps/mesa-progs
	sys-libs/ncurses
	x11-drivers/nvidia-drivers[tools]
	dev-libs/openssl-compat:1.0.0
	x11-libs/pango
	x11-libs/pixman
	media-libs/portaudio
	dev-db/postgresql
	dev-lang/python
	dev-qt/qtdeclarative
	dev-qt/qtmultimedia
	dev-qt/qtwebengine
	dev-qt/qtxmlpatterns
	dev-lang/R
	app-text/tesseract
	sys-libs/zlib
	virtual/libcrypt"

# Mathematica comes with a lot of bundled stuff. We should place here only what we
# explicitly override with LD_PRELOAD.
RDEPEND="
	media-libs/freetype
	${DEPEND}
"

# we need this a few times
MPN="Mathematica"
MPV=$(ver_cut 1-2)
M_BINARIES="MathKernel Mathematica MathematicaScript WolframKernel WolframScript math mathematica mcc wolfram"
M_TARGET="opt/Wolfram/${MPN}/${MPV}"

# we might as well list all files in all QA variables...
QA_PREBUILT="opt/*"

S=${WORKDIR}

src_unpack() {
	/bin/sh "${DISTDIR}/${A}" --nox11 --keep --target "${S}/unpack" -- "-help" || die
}

src_prepare() {
	default

	pushd "${S}/unpack" > /dev/null || die

	/bin/sh "Unix/Installer/MathInstaller" -auto "-targetdir=${S}/${M_TARGET}" "-execdir=${S}/opt/bin" || die

	popd > /dev/null || die

	# unpack wolframscript
	bsdtar -xf ${M_TARGET}/SystemFiles/Installation/wolframscript_*.deb data.tar.xz
	tar -xf data.tar.xz
	rm data.tar.xz
}

src_install() {
	local ARCH='-x86-64'

	if ! use doc; then
		einfo "Removing documentation"
		rm -r "${S}/${M_TARGET}/Documentation"
	fi

	einfo 'Removing MacOS- and Windows-specific files'
	pushd "${S}/${M_TARGET}" > /dev/null || die
	find AddOns SystemFiles -type d -\( -name Windows -o -name Windows-x86-64 \
		-o -name MacOSX -o -name MacOSX-x86-64 -\) | xargs rm -rf
	popd > /dev/null || die

	# move all over
	mv "${S}"/{opt,usr} "${D}"/ || die

	# the autogenerated symlinks point into sandbox, regenerate
	pushd "${D}/opt/bin" > /dev/null || die
	ls | while read name
	do
		ln -sf ../Wolfram/${MPN}/${MPV}/Executables/${name} || die
	done
	ln -sf ../Wolfram/WolframScript/bin || die
	cp * "${D}/usr/bin/"
	popd > /dev/null || die

	# fix some embedded paths and install desktop files
	pushd "${D}/${M_TARGET}/SystemFiles/Installation" > /dev/null || die
	sed -e "s:${S}::g" -e 's:^\t\t::g' -i wolfram-mathematica12.desktop
	echo "Categories=Physics;Science;Engineering;2DGraphics;Graphics;Math;NumericalAnalysis;DataVisualization;" >> wolfram-mathematica12.desktop
	domenu wolfram-mathematica12.desktop
	mkdir -p ${D}/usr/share/desktop-directories
	cp wolfram-all.directory ${D}/usr/share/desktop-directories/
	popd > /dev/null || die

	# install mime types
	insinto /usr/share/mime/application
	for filename in $(find "${D}/${M_TARGET}/SystemFiles/Installation" -name "application-*.xml"); do
		basefilename=$(basename "${filename}")
		mv "${filename}" "${T}/${basefilename#application-}"
		doins "${T}/${basefilename#application-}"
	done

	# install icons
	pushd "${D}/${M_TARGET}/SystemFiles/FrontEnd/SystemResources/X" > /dev/null || die
    for i in 32 64 128; do
		mkdir -p "${D}/usr/share/icons/hicolor/${i}x${i}/apps"
        cp App-${i}.png "${D}/usr/share/icons/hicolor/${i}x${i}/apps/wolfram-mathematica.png"
        for mimetype in $(ls vnd.* | cut -d '-' -f1 | uniq); do
            cp ${mimetype}-${i}.png ${D}/usr/share/icons/hicolor/${i}x${i}/mimetypes/application-${mimetype}.png
        done
    done

	# fix doc install path
	pushd "${D}/usr/share/doc" > /dev/null || die
	dodoc wolframscript/copyright
	rm -rf wolframscript
	popd > /dev/null || die
}

pkg_nofetch() {
	einfo "Please place the Wolfram Mathematica installation file ${SRC_URI}"
	einfo "in your \$\{DISTDIR\}."
	einfo "Note that to actually run and use Mathematica you need a valid license."
	einfo "Wolfram provides time-limited evaluation licenses at ${HOMEPAGE}"
}
